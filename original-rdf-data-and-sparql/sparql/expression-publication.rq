PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX frbroo: <http://iflastandards.info/ns/fr/frbr/frbroo/>
PREFIX event: <http://e-editiones.ch/ontology/event#>
PREFIX human: <http://e-editiones.ch/ontology/human#>
PREFIX creating: <http://e-editiones.ch/ontology/creating#>
PREFIX language: <http://e-editiones.ch/ontology/language#>
PREFIX calendar: <http://e-editiones.ch/ontology/calendar#>
PREFIX text: <http://e-editiones.ch/ontology/text#>
PREFIX text-expression: <http://e-editiones.ch/ontology/text-expression#>
PREFIX publishing: <http://e-editiones.ch/ontology/publishing#>
PREFIX delille: <http://e-editiones.ch/ontology/delille#>


# RETRIEVE CITING AND TRANSLATION EXPRESSIONS INCLUDING ARTICLES, WITH PERSON NAME, ROLES AND PUBLICATION TIME:

CONSTRUCT {
	?expression
		a ?Expression;
		text-expression:hasTitleLiteral ?textExpressionTitleLiteral;
		?outputHasAgent ?person;
		event:outputInvolvesRole ?role;
		text-expression:isPartOfExpression ?articleTanslatedVersePoem;
		publishing:isPublishedIn ?publication;
		publishing:isRepublishedIn ?republication;
		a creating:AnonymousCreation.
	?person
		a ?Person;
		human:hasNameLiteral ?nameLiteral;
		human:hasGivenNameLiteral ?givenNameLiteral;
		human:hasFamilyNameLiteral ?familyNameLiteral;
		human:personHasRole ?role.
	?role a ?Role.
	?republication a publishing:Republication.
	?articleTanslatedVersePoem a delille:TranslatedVersePoem.
 	?publication
 		a ?Publication;
  		publishing:hasYearPeriod ?yP;
  		publishing:hasPartitionedPublishing ?pP;
  		publishing:isSerialIssueOfVolume ?serialVolume.
  	?Publication rdfs:subClassOf frbroo:F5_Item.
  	?yP
		calendar:hasGregorianStartDate ?sD;
		calendar:hasGregorianEndDate ?eD;
		calendar:hasStartJulianDayNumber ?sJDN;
		calendar:hasEndJulianDayNumber ?eJDN.
	?pP
		a publishing:PartitionedPublishing;
		?hasPeriod ?period.
	?period
		calendar:hasGregorianStartDate ?sDPP;
		calendar:hasGregorianEndDate ?eDPP;
		calendar:hasStartJulianDayNumber ?sJDNPP;
		calendar:hasEndJulianDayNumber ?eJDNPP.
	?serialVolume
		a publishing:SerialVolume;
		publishing:hasTitleLiteral ?t
	}

WHERE {
	?expression
		a ?Expression;
		text-expression:hasTitleLiteral ?textExpressionTitleLiteral;
		?outputHasAgent ?person; # also retrieve text-expression:hasPossibleAuthor and human:UnidentifiedPerson
		event:outputInvolvesRole ?role;
		publishing:isPublishedIn ?publication.
	?person
		a ?Person;
		human:personHasRole ?role.
	?role a ?Role.
	?publication a ?Publication.
	?Publication rdfs:subClassOf frbroo:F5_Item.
	OPTIONAL {
		?expression publishing:isRepublishedIn ?republication.
		?republication a publishing:Republication.
		}
	OPTIONAL {
		?expression
			text-expression:isPartOfExpression ?articleTanslatedVersePoem.
		?articleTanslatedVersePoem a delille:TranslatedVersePoem
		}
  	OPTIONAL {?person human:hasNameLiteral ?nameLiteral}
  	OPTIONAL {?person human:hasGivenNameLiteral ?givenNameLiteral}
  	OPTIONAL {?person human:hasFamilyNameLiteral ?familyNameLiteral}
  	OPTIONAL {?expression a creating:AnonymousCreation}
	OPTIONAL {
		?publication publishing:hasYearPeriod ?yP.
		?yP
			calendar:hasGregorianStartDate ?sD;
			calendar:hasGregorianEndDate ?eD;
			calendar:hasStartJulianDayNumber ?sJDN;
			calendar:hasEndJulianDayNumber ?eJDN.
		OPTIONAL {
			?publication publishing:isSerialIssueOfVolume ?serialVolume.
			?serialVolume
				a publishing:SerialVolume;
				publishing:hasTitleLiteral ?t			
			}
		}
	OPTIONAL {
		?publication publishing:hasPartitionedPublishing ?pP.
		?pP
			a publishing:PartitionedPublishing;
			?hasPeriod ?period.
		?period
			calendar:hasGregorianStartDate ?sDPP;
			calendar:hasGregorianEndDate ?eDPP;
			calendar:hasStartJulianDayNumber ?sJDNPP;
			calendar:hasEndJulianDayNumber ?eJDNPP.
		}

	}